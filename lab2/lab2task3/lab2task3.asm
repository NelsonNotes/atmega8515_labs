;Соединения на плате STK500: sw1-PD2, sw2-PD3, LED0-PB0
;*********************************************************************
.include "m8515def.inc" ;файл определений для ATmega8515
.def temp = r16 ;временный регистр
.def led_state = r17 ;регистр управляющего светодиодами слова
.def job = r18 ;регистр флага работы
.equ sw1 = 3 ;3-й бит порта PD
.equ sw2 = 0 ;0-й бит порта PE
.org $000
;***Таблица векторов прерываний, начиная с адреса $000***
rjmp INIT ;обработка сброса
.org $002 ;сдвиг по памяти такой, что следующая команда лежит по адресу вектора прерываний для INT1
rjmp job_set ;на обработку запроса INT1
.org $00D ;сдвиг по памяти такой, что следующая команда лежит по адресу вектора прерываний для INT2
rjmp job_clr ;на обработку запроса INT2
;***Инициализация SP, портов, регистра маски***
INIT: 
ldi temp,$5F ;установка
out SPL,temp ; указателя стека
ldi temp,$02 ; на последнюю
out SPH,temp ; ячейку ОЗУ

ldi led_state, 0b11111110 ; инициализация управляющего слова
sec ; установка флага переполнения C в 1, чтобы при первом ROL управляющее слово дополнялось справа единицей
cbr job,0 ; инициализация флага работы 

ser temp ;инициализация выводов
out DDRB,temp ; порта PB на вывод
out PORTB,temp ;погасить СД

cbi DDRD,sw1; инициализация бита PD3 порта PD на ввод
sbi PORTD,sw1; включение подтягивающего резистора порта PD

cbi DDRE,sw2; инициализация бита PE0 порта PE на ввод
sbi PORTE,sw2; включение подтягивающего резистора порта PE


ldi temp,((1<<INT2)|(1<<INT1));разрешение прерываний
out GICR,temp ; в 5,7 битах регистра маски GICR
ldi temp,0
out MCUCR,temp ; обработка INT1 по низкому уровню
out EMCUCR,temp ; обработка INT2 по отрицательному фронту
sei ;глобальное разрешение прерываний

MAIN: ;основной цикл работы процессора
sbrs job,0 ;проверка состояния флага job
rjmp MAIN

out PORTB,led_state
rcall delay1
rol led_state
rjmp MAIN

job_set:
ldi job,1 ; установка флага работы
reti
job_clr:
clr job ; сброс флага работы
reti

delay1:
;для подпрограммы задержки 1 c
ldi r19,100
d1: ldi r20,101 
d2: ldi r21,135
d3: dec r21
brne d3
dec r20
brne d2
dec r19
brne d1
ret
